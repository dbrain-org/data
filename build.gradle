apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = "org.dbrain"
archivesBaseName = "dbrain-data"
version = '0.13-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.5.3',
            'com.fasterxml.jackson.core:jackson-annotations:2.5.3',
            'com.fasterxml.jackson.core:jackson-databind:2.5.3'

    testCompile 'junit:junit:4.+'
}

idea {
    project {
        //if you want to set specific jdk and language level
        jdkName = '1.8'
        languageLevel = '1.8'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'dbrain-data'
                packaging 'jar'
                description 'Defines concepts and classes that are at the core of data manipulation and definition.'
                url 'http://dbrain.org'

                scm {
                    connection 'scm:git:git@github.com:dbrain-org/dbrain-data.git'
                    developerConnection 'scm:git:git@github.com:dbrain-org/dbrain-data.git'
                    url 'https://github.com/dbrain-org/dbrain-data'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'eric.poitras'
                        name 'Eric Poitras'
                        email 'eric.poitras@gmail.com'
                    }
                }
            }
        }
    }
}
